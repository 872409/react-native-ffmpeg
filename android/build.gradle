def safePackageName(prop) {
    prop.replace("-lts", "")
}

def safePackageVersion(prop, version, ltsVersion) {
    prop.contains("-lts") ? ltsVersion + ".LTS" : version
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

apply plugin: 'com.android.library'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 32
        versionName "0.3.2"
        project.archivesBaseName = "react-native-ffmpeg"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    maven { url "$rootDir/node_modules/react-native/android" }
    mavenCentral()
    jcenter()
    maven { url 'https://maven.google.com' }
}

dependencies {
    implementation 'com.arthenica:mobile-ffmpeg-' + safePackageName(safeExtGet('reactNativeFFmpegPackage', 'https')) + ':' + safePackageVersion(safeExtGet('reactNativeFFmpegPackage', 'https'), '4.2.1', '4.2')
    implementation 'com.facebook.react:react-native:+'
}
