static String safePackageName(String prop) {
    prop.replace("-lts", "")
}

static String safePackageVersion(String prop, String version, String ltsVersion) {
    prop.contains("-lts") ? ltsVersion + ".LTS" : version
}

String safeExtGet(String prop, String fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

apply plugin: 'com.android.library'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion safeExtGet('reactNativeFFmpegPackage', 'https').contains("-lts") ? 16 : 24
        targetSdkVersion 28
        versionCode 33
        versionName "0.3.3"
        project.archivesBaseName = "react-native-ffmpeg"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    maven { url "$rootDir/node_modules/react-native/android" }
    mavenCentral()
    jcenter()
    maven { url 'https://maven.google.com' }
}

dependencies {
    implementation 'com.arthenica:mobile-ffmpeg-' + safePackageName(safeExtGet('reactNativeFFmpegPackage', 'https')) + ':' + safePackageVersion(safeExtGet('reactNativeFFmpegPackage', 'https'), '4.2.2', '4.2.2')
    implementation 'com.facebook.react:react-native:+'
}
